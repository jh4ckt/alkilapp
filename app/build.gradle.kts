import java.io.FileInputStream
import java.util.Properties

plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("com.google.gms.google-services")
}

val localProps = Properties().apply {
    val f = rootProject.file("local.properties")
    if (f.exists()) FileInputStream(f).use { load(it) }
}
val mapsApiKey: String = localProps.getProperty("MAPS_API_KEY") ?: "TU_API_KEY_AQUI"

android {
    namespace = "com.alkilapp"
    compileSdk = 36

    defaultConfig {
        applicationId = "com.alkilapp"
        minSdk = 24
        targetSdk = 36
        versionCode = 15
        versionName = "1.33"
        manifestPlaceholders["MAPS_API_KEY"] = mapsApiKey
    }

    buildTypes {
        release {
            isMinifyEnabled = false
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    // Google Maps SDK & Location Services
    implementation("com.google.android.gms:play-services-maps:18.2.0")
    implementation("com.google.android.gms:play-services-location:21.1.0")
    implementation("com.google.android.gms:play-services-auth:21.2.0")
    // Places SDK (New): autocomplete de direcciones al publicar un inmueble
    implementation("com.google.android.libraries.places:places:3.5.0")

    // Google Play Billing: compras in-app (destacados)
    implementation("com.android.billingclient:billing:7.0.0")

    // Firebase: Firestore (base de datos) + Authentication (registro de usuarios)
    implementation(platform("com.google.firebase:firebase-bom:33.1.1"))
    implementation("com.google.firebase:firebase-firestore")
    implementation("com.google.firebase:firebase-auth")

    // Android Jetpack & Material Design 3
    implementation("androidx.core:core-ktx:1.13.1")
    implementation("androidx.appcompat:appcompat:1.7.0")
    implementation("com.google.android.material:material:1.12.0")
    implementation("androidx.constraintlayout:constraintlayout:2.1.4")
    implementation("androidx.recyclerview:recyclerview:1.3.2")
    implementation("androidx.viewpager2:viewpager2:1.1.0")
}